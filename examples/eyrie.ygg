#    Horizontal: 4 '-' per ft
#    Vert: 2 '|' per ft
#    Reference is upper-left corner.
#    X is left-to-right.
#    Y is top-to-bottom.
#    Z is floor-to-ceiling.
#
#    ---------------------------------------------------------------------------------------------------+
#    |                                        |        .            *                                   |
#    |                                        |        .                                                |
#    |                                        |        .                                                |
#    |                                        |        .                                                |
#    |                                        |        .                                                |
#    |                                        |        .                                                |
#    |                                        |        .                                                |
#    |                                        |        .                                                |
#    |         Office                         |        .                 Bedroom                        |
#    |            10ftx13ft                   |________.                    12ftx10ft                   |
#    |                 (+2ft for closet)      .        |                       (+2ft for closet)        |
#    |                                        .        |                                                |
#    |                                        .        |                                                |
#    |                                        .        |                                                |
#    |                                        .        |                                                |
#    |                                        .        |                                                |
#    |                                        .        |                                                |
#    |                                        .        |                                                |
#    |                                        +________+@@@@@@@@@@--------------------------------------+
#    |                                        @                         @                               |
#    |                                        @        Hall             @                               |
#    |                                        @          7' x 6'        @                               |
#    |                                        @                         @                               |
#    |@@@@@@-------------------------------------------+          +-----+        Bathroom               |
#    @                                                 @          @     |                               |
#    @                                                 @          @     |                               |
#    @  Entry                                          @          @     |                               |
#    @    42" x 42"                                    @          @     |                               |
#    @                                                 @          @     |                               |
#    +--------------                                   +@@@@@@@@@@+-----+-------------------------------+
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |          Kitchen                               |
#    |                                                 |             12'x8'                             |
#    |     Living Room                                 |                                                |
#    |        13' x 19'9"                              |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 |                                                |
#    |                                                 +----------------@@@@@@@@@@@@---+@@@@@@@@@@------+
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                      Dining Room                |   Utility      |
#    |                                                         7'6"x8'6"               |     4'6"x8'6"  |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    |                                                                                 |                |
#    +-------------------------------------@@@@@@@@@@@@--------------------------------+----------------+

palette
    hue
        global-on
            on        <- "bhs(254, 34495, 254)"
            low       <- "bhs(64, 34495, 254)"
            moonlight <- "none"
            default   <- "bhs(128, 34495, 254)"
            off       <- "none"
        global-off
            on        <- "none"
            low       <- "none"
            moonlight <- "none"
            default   <- "none"
            off       <- "none"
        emergency
            on        <- "bhs(254, 0, 254)"
            low       <- "bhs(254, 0, 254)"
            moonlight <- "bhs(254, 0, 254)"
            default   <- "rgb(254, 0, 254)"
            off       <- "bhs(254, 0, 254)"

    glow-button
        global-on
            on        <- "none"
            low       <- "rgb(0, 0, 128)"
            moonlight <- "rgb(206, 92, 0)"
            default   <- "bhs(128, 34495, 254)"
            off       <- "rgb(0, 0, 1)"
        global-off
            on        <- "none"
            low       <- "none"
            moonlight <- "none"
            default   <- "none"
            off       <- "none"
        emergency
            on        <- "bhs(254, 0, 254)"
            low       <- "bhs(254, 0, 254)"
            moonlight <- "bhs(254, 0, 254)"
            default   <- "bhs(254, 0, 254)"
            off       <- "bhs(254, 0, 254)"

    glow-effect
        on        <- "solid"
        low       <- "wave"
        moonlight <- "flame"
        default   <- "solid"
        off       <- "solid"

    hue-stream
        on        <- "bhs(254, 34495, 254)"
        low       <- "bhs(64, 34495, 254)"
        moonlight <- "bhs(128, 34495, 254)"
        default   <- "bhs(254, 34495, 254)"
        off       <- "none"

    hue-highlight
        on        <- "none"
        low       <- "none"
        moonlight <- "bhs(254, 47000, 254)"
        default   <- "bhs(254, " + str(/meta/minute-tic * 1092) + ", 254)"
        off       <- "none"

semantics
    knifeswitch-emergency
        up <- "global-on"
        open <- "global-off"
        down <- "emergency"
    knifeswitch-control
        up <- "global-on"
        open <- "global-off"
        down <- "global-off"
    glowswitch
        0 <- "on"
        1 <- "low"
        2 <- "moonlight"
        3 <- "off"
ctrl <- /semantics/knifeswitch-control/{/rooms/livingroom/knifeswitch.eyrie/position}
emer <- /semantics/knifeswitch-emergency/{/rooms/livingroom/knifeswitch.eyrie/position}

rooms
    bedroom @12'x0' <>12'x10'
        closet @-2'x0' <>2'x5'

        bedroom-lightswitch.eyrie
            ^legacy-mcu
            ip <-"10.0.5.40"
            default <- "off"
        color0 <- ./bedroom-lightswitch.eyrie

        bedroom-lightswitch-1.eyrie
            most_recent_button_press ^redstone
                default <- 3
            color $redstone <-/palette/glow-button/{/emer}/{../color}
            effect $redstone <-/palette/glow-effect/{../color}
        color1 <- /semantics/glowswitch/{./bedroom-lightswitch-1.eyrie/most_recent_button_press}

        bedroom-lightswitch-2.eyrie
            most_recent_button_press ^redstone
                default <- 3
            color $redstone <-/palette/glow-button/{/emer}/{../color}
            effect $redstone <-/palette/glow-effect/{../color}
        color2 <- /semantics/glowswitch/{./bedroom-lightswitch-2.eyrie/most_recent_button_press}

        color <- ./color0 :: ./color1 :: ./color2
        bedroom-bookshelf0 $hue @10'x1' <-/palette/hue/{/emer}/{./color}
        bedroom-bookshelf1 $hue @4'x8'  <-/palette/hue/{/ctrl}/{./color}
        bedroom-dresser    $hue @10'x2' <-/palette/hue/{/emer}/{./color}
        bedroom-tree0      $hue @1'x2'  <-/palette/hue/{/ctrl}/{./color}
        bedroom-tree1      $hue @1'x1'  <-/palette/hue/{/ctrl}/{./color}
        bedroom-tree2      $hue @2'x1'  <-/palette/hue/{/emer}/{./color}
        bedroom-ceiling    $hue @6'x6'  <-/palette/hue/{/ctrl}/{./color}

    office @0'x0' <>10'x13'
        closet @10'x5' <>2'x5'

        test-switch
            ^legacy-mcu
            ip <- "127.0.0.1"
            default <- "default"

        office-lightswitch.eyrie
            most_recent_button_press ^redstone
                default <- 3
            color $redstone <-/palette/glow-button/{/emer}/{../color}

        color <- /semantics/glowswitch/{./office-lightswitch.eyrie/most_recent_button_press}

        office-ceiling1 $hue @5'x6'  <-/palette/hue/{/emer}/{./color}
        office-ceiling2 $hue @4'x7'  <-/palette/hue/{/emer}/{./color}
        office-desk0    $hue @11'x0' <-/palette/hue-highlight/{./color}
        office-stream   $hue @11'x0' <-/palette/hue-stream/{./test-switch}

    hall @10'x10' <>7'x6'
        closet @6'x3' <>1'x3'
        color <-\
            if ../bedroom/color == "off" || ../livingroom/color == "off":
                "off"
            elif ../bedroom/color == "on" || ../livingroom/color == "on" || ../kitchen/color == "on" || ../office/color == "on":
                "on"
            else:
                "low"
        hall-ceiling0 $hue @4'x2' <-/palette/hue/{/emer}/{./color}
        hall-ceiling1 $hue @5'x3' <-/palette/hue/{/emer}/{./color}

    bathroom @17'x10' <>7'x6'

    utility @20'6"x24' <>4'6"x8'6"
        color <- ../kitchen/kitchen-lightswitch.eyrie
        utility-ceiling $hue @2'x4' <-/palette/hue/{/emer}/{./color}

    kitchen @13'x16' <>11'x8'
        kitchen-lightswitch.eyrie
            ^legacy-mcu
            ip <- "10.0.5.41"
            default <- "off"
        color <- ./kitchen-lightswitch.eyrie
        kitchen-sink      $hue @9'x1' <-./sink-palette/{./color}
        kitchen-ceiling0  $hue @2'x6' <-/palette/hue/{/ctrl}/{./color}
        kitchen-ceiling1  $hue @3'x5' <-/palette/hue/{/emer}/{./color}
        kitchen-ceiling2  $hue @4'x4' <-/palette/hue/{/ctrl}/{./color}
        kitchen-ceiling3  $hue @5'x3' <-/palette/hue/{/emer}/{./color}
        kitchen-ceiling4  $hue @6'x2' <-/palette/hue/{/ctrl}/{./color}
        sink-palette
            on        <- /palette/hue/{/ctrl}/on
            low       <- /palette/hue/{/ctrl}/low
            moonlight <- /palette/hue/{/ctrl}/on
            default   <- /palette/hue/{/ctrl}/default
            off       <- /palette/hue/{/ctrl}/off

    livingroom @0'x13' <>13'x19'9"
        #knifeswitch.eyrie @3'x3'
        #    position ^redstone
        knifeswitch.eyrie @3'x3'
            position <- "up"

        livingroom-lightswitch.eyrie @0'x0'
            ^legacy-mcu
            ip <- "10.0.5.42"
            default <- "on"
        color <- ./livingroom-lightswitch.eyrie
        livingroom-couch    $hue @1'x6'   <-/palette/hue/{/emer}/{./color}
        livingroom-torch    $hue @1'x10'  <-/palette/hue/{/ctrl}/{./color}
        livingroom-tower0   $hue @10'x3'  <-\
            if ./color == "off" && ../bedroom/color == "moonlight":
                /palette/hue/{/emer}/low
            else:
                /palette/hue/{/emer}/{./color}
        livingroom-tower1   $hue @10'x2'  <-/palette/hue/{/ctrl}/{./color}
        livingroom-tower2   $hue @10'x1'  <-/palette/hue/{/ctrl}/{./color}
        livingroom-curtain1 $hue @10'x15' <-/palette/hue/{/emer}/{./color}
        livingroom-curtain2 $hue @11'x16' <-/palette/hue/{/ctrl}/{./color}
        livingroom-curtain3 $hue @12'x17' <-/palette/hue/{/ctrl}/{./color}

    diningroom @13'x24' <>7'6"x8'6"
        color <- ../livingroom/livingroom-lightswitch.eyrie
        diningroom-ceiling $hue @3'x4'

meta
    hue-bridge
        $hue-bridge
        address <- "hue-bridge.eyrie"
        username <- "MasterControlProgram"

    minute-tic
        ^clock
        interval <- "minute"
        wrap <- "hourly"